{{- /* vim: set ft=zsh : */ -}}

export XDG_CONFIG_HOME=$HOME/.config
export XDG_CACHE_HOME=$HOME/.cache
export XDG_DATA_HOME=$HOME/.local/share

path=(
  $HOME/bin(N-/)
  $XDG_DATA_HOME/aquaproj-aqua/bin/(N-/)
  $HOME/.local/bin(N-/)
  $path
)
typeset -U PATH


# plugins
source $XDG_CONFIG_HOME/zsh/plugins/antidote/antidote.zsh
antidote load $XDG_CONFIG_HOME/zsh/zsh_plugins

###########
# bindkey #
###########
# vi
bindkey -v
bindkey -M viins '^A' beginning-of-line
bindkey -M viins '^E' end-of-line
bindkey -M viins '^N' down-line-or-history
bindkey -M viins '^P' up-line-or-history
# own
bindkey -M viins '^R' zsh-history

##############
# completion #
##############
autoload -U compinit
compinit

autoload colors
zstyle ':completion:*' completer _expand_alias _complete _ignored

zstyle ':completion:*:descriptions' format '%F{blue} %B%d%b%f'
zstyle ':completion:*:warnings' format '%F{yello} No matches for: %d%f'

zstyle ':completion:*' group-name ''
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' menu select=2
zstyle ':completion:*' verbose yes

###########
# history #
###########
HISTFILE=$HOME/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

setopt appendhistory
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_no_store
setopt hist_reduce_blanks
setopt share_history

############
# commands #
############

{{ if lookPath "aqua" -}}
export AQUA_GLOBAL_CONFIG=$XDG_CONFIG_HOME/aqua/config.yaml
{{- end }}

{{ if lookPath "mise" -}}
{{ output "mise" "activate" "zsh" }}
{{ output "mise" "completion" "zsh" }}
{{- end }}

{{ if lookPath "fzf" -}}
export FZF_DEFAULT_OPTS='--reverse --border'
{{- end }}

{{ if lookPath "direnv" -}}
{{ output "direnv" "hook" "zsh" }}
{{- end }}

{{ if lookPath "starship" -}}
{{ output "starship" "init" "zsh" }}
{{- end }}

{{ if lookPath "zoxide" -}}
{{ output "zoxide" "init" "zsh" }}
{{- end }}

{{ if lookPath "nvim" -}}
alias v=nvim
export EDITOR=nvim
{{- end }}

###############
# my commands #
###############
function zle-keymap-select () {
  case $KEYMAP in
    vicmd) echo -ne '\e[1 q';;
    viins|main) echo -ne '\e[5 q';;
  esac
}
zle -N zle-keymap-select

function zle-line-init() {
  zle -K viins
  echo -ne "\e[5 q"
}
zle -N zle-line-init

echo -ne '\e[5 q'
function preexec() { echo -ne '\e[5 q'; }

function zsh-history() {
  BUFFER=`history -n 1 | fzf --query "$BUFFER"`
  CURSOR=$#BUFFER
  zle reset-prompt
}
zle -N zsh-history

function ssm() {
  local profile=$(aws configure list-profiles | fzf)
  if [ -z "$profile" ] ; then; return; fi
  local target=$(aws --profile $profile ec2 describe-instances --filters 'Name=instance-state-name,Values=running' --query 'Reservations[*].Instances[*].[Tags[?Key==`Name`]|[0].Value, InstanceId]' --output text | fzf | cut -f2)
  if [ -z "$target" ] ; then; return; fi
  aws --profile $profile ssm start-session --target $target
}

function zr() {
  cd $(ghq list -p  | fzf)
}

function tm() {
  if ! tmux has-session 2>/dev/null; then
    tmux new-session -s main -c "${HOME}" -d
  fi

  target=$(
    tmux list-sessions -F "#S" | fzf \
      --header='Ctrl+C: new | Ctrl-D: delete' \
      --bind='ctrl-c:reload(zoxide query --list)' \
      --bind='ctrl-d:execute(tmux kill-session -t {1})+reload(tmux list-sessions -F "#S")'
  )

  if ! tmux has-session -t "${target}" 2>/dev/null; then
    session_name=$(basename "${target}")
    tmux new-session -s $session_name -c "${target}" -d
    target="${session_name}"
  fi

  if [ -z "${TMUX}" ]; then
    tmux attach-session -t "${target}"
  else
    tmux switch-client -t "${target}"
  fi
}

#########
# alias #
#########
alias ..='cd ..'
alias t='tmux attach || tmux new'
alias ch='chezmoi'
alias cha='chezmoi apply -v'
alias chc='cd $(chezmoi source-path)'
alias chd='chezmoi diff'
alias chu='chezmoi update -v'
alias ag='aqua generate'
alias ai='aqua install --all --only-link'
alias dc='docker compose'
alias gc='git commit -v'
alias gcl='git clean -fd'
alias gf='git fetch'
alias ld='lazydocker'
alias lg='lazygit'
alias resh='exec $SHELL -l'

{{ if lookPath "eza" -}}
alias ls='eza --git --git-repos'
alias ll='eza --git --git-repos --header --long'
{{ else }}
alias ls='ls --color'
alias ll='ls -lAF'
{{- end }}

alias -g G='| grep --color=auto'
