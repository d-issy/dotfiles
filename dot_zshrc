###############
# environment #
###############
export XDG_CONFIG_HOME=$HOME/.config
export XDG_CACHE_HOME=$HOME/.cache
export XDG_DATA_HOME=$HOME/.local/share


##############
# completion #
##############
autoload -U compinit
compinit

autoload colors

zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' list-colors ''

###########
# history #
###########
HISTFILE=$HOME/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

setopt appendhistory
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_no_store
setopt hist_reduce_blanks
setopt share_history

############
# commands #
############

# asdf
if [ -d $HOME/.asdf ]; then
  . $HOME/.asdf/asdf.sh

  # golang package bin
  ASDF_GOLANG_PACKAGE_BIN=$(asdf where golang)/packages/bin
fi

# direnv
if [ -d $HOME/.asdf ]; then
  eval "$(direnv hook zsh)"
fi

# fzf
if (( $+commands[fzf] )); then
  export FZF_DEFAULT_OPTS='--reverse --height 40%'
fi

# starship
if (( $+commands[starship] )); then
  eval "$(starship init zsh)"
fi

# thefuck
if (( $+commands[thefuck] )); then
  eval $(thefuck --alias)
fi

# zoxide
if (( $+commands[zoxide] )); then
  eval "$(zoxide init zsh)"
fi

# kubectl completions
if builtin command -v kubectl &>/dev/null; then
  source <(kubectl completion zsh)
fi

# zsh syntax highlight
if [ -e $XDG_CONFIG_HOME/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]; then
  source $XDG_CONFIG_HOME/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

# zsh autosuggestions
if [ -e $XDG_CONFIG_HOME/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh ]; then
  source $XDG_CONFIG_HOME/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

# EDITOR
if (( $+commands[nvim] )); then
  alias v=nvim
  export EDITOR=nvim
elif (( $+commands[vim] )); then
  alias v=vim
  export EDITOR=vim
fi

################
# own commands #
################
zsh-history() {
  if (( $+commands[fzf] )); then
    BUFFER=`history -n 1 | fzf --query "$BUFFER"`
    CURSOR=$#BUFFER
    zle reset-prompt
  else
    zle history-incremental-search-backward
  fi
}
zle -N zsh-history

ssm() {
  local profile=$(aws configure list-profiles | fzf)
  local target=$(aws --profile $profile ec2 describe-instances --filters 'Name=instance-state-name,Values=running' --query 'Reservations[*].Instances[*].[Tags[?Key==`Name`]|[0].Value, InstanceId]' --output text | fzf | cut -f2)
  if [ -n "$target" ] ; then
    aws --profile $profile ssm start-session --target $target
  fi
}

###########
# bindkey #
###########

# vi
bindkey -v
bindkey -M viins '^A' beginning-of-line
bindkey -M viins '^E' end-of-line
bindkey -M viins '^N' down-line-or-history
bindkey -M viins '^P' up-line-or-history

# own
bindkey -M viins '^R' zsh-history


########
# path #
########

path=(
  $HOME/bin(N-/)
  $HOME/.local/bin(N-/)
  $ASDF_GOLANG_PACKAGE_BIN(N-/)
  $path
)

fpath=(
  $HOME/.config/zsh/plugins/zsh-completions/src(N-/)
  $HOME/.asdf/completions(N-/)
  $fpath
)

# vim: set ft=zsh :
